метод ОбработкаЗапроса(Запрос: HttpСервисЗапрос)
    пер Ответ = Запрос.Ответ
    Ответ.Заголовки.Установить("Content-Type", "text/plain")
    Ответ.УстановитьТело("Ok") 
    //ДатаВремя.Сейчас().ВСтроку()
;


метод BotPOSTОбработкаЗапроса(Запрос: HttpСервисЗапрос)
    знч SpecialString = Запрос.Параметры.ПолучитьПервый("SpecialString")
    если МодульТелеграм.ПолучитьSpecialString() != SpecialString
        пер Ответ = Запрос.Ответ
        Ответ.УстановитьТело("Неверный протокол")
        возврат
    ;

    знч ТелоЗапроса = Запрос.Тело.ПрочитатьКакСтроку()

    новый ExecutionEvent(Details = ТелоЗапроса).Записать()

    знч ЧтениеJson = new ЧтениеJson(ТелоЗапроса)
    знч ТелоКакСоответствие = ЧтениеJson.ПрочитатьСодержимоеКакСоответствие()

    пер Message: Соответствие<Строка, Объект?>

    если ТелоКакСоответствие.СодержитКлюч("message")
        Message = ТелоКакСоответствие.Получить("message") как Соответствие<Строка, Объект?>
    иначе если ТелоКакСоответствие.СодержитКлюч("edited_message")
        Message = ТелоКакСоответствие.Получить("edited_message") как Соответствие<Строка, Объект?>
    иначе
        пер Ответ = Запрос.Ответ
        Ответ.УстановитьТело("Не найдено сообщение")
        возврат
    ;

    пер ТекстСообщения: Строка
    пер ТекстДополнительный: Строка = ""

    если Message.СодержитКлюч("voice")
        знч Признак = Message.Получить("voice") как Соответствие<Строка, Объект?>
        знч ИдФайла = Признак.Получить("file_id") как Строка
        
        ТекстСообщения = МодульТелеграм.СкачатьИРазобратьРечь(ИдФайла)
        ТекстДополнительный = ", %ТекстСообщения"

    иначе если Message.СодержитКлюч("text")
        ТекстСообщения = Message.Получить("text") как Строка

    иначе
        пер Ответ = Запрос.Ответ
        Ответ.УстановитьТело("Не найдено сообщение")
        возврат
    ;

    знч Chat = Message.Получить("chat") как Соответствие<Строка, Объект?>
    знч idЧата = Chat.Получить("id") как Число

    /*
     *     пер ИмяПользователя: Строка = ""
     *     если Chat.СодержитКлюч("first_name")
     *         ИмяПользователя = Chat.Получить("first_name") как Строка
     *     ;    
     */
    знч ДатаСообщенияЧислом = Message.Получить("date") как Число
    знч ДатаСообщения = Момент.ИзОтметкиВремениUnix(ДатаСообщенияЧислом).ВДатаВремя(ЧасовойПояс.Текущий())

    знч s = АнализДат.Разобрать(ТекстСообщения)
    знч d = АнализДат.DateTimeParts.from(s, ДатаСообщения, 0)
    знч ДатаСообщенияРазобранная = d.parse_data()

    пер ОтветСтрокой: Строка

    если ДатаСообщенияРазобранная == Неопределено
        ОтветСтрокой = МодульТелеграм.ОтправитьТекстовоеСообщение(idЧата,
                        "${ДатаСообщения|дд.ММ.гггг ЧЧ:мм}, не определено время %ТекстДополнительный")

    иначе
        
        знч Поля = новый СтруктураПолученныхПолей(СтрокаИсходная = ТекстСообщения,
            ДатаРегистрации = ДатаСообщения,
            ДатаНапоминания = ДатаСообщенияРазобранная,
            ИдЧата = idЧата)

        СправочникНапоминаний.ОбработатьНапоминание(Поля)

        ОтветСтрокой = МодульТелеграм.ОтправитьТекстовоеСообщение(idЧата,
                        "${ДатаСообщения|дд.ММ.гггг ЧЧ:мм}, определено время: ${ДатаСообщенияРазобранная|дд.ММ.гггг ЧЧ:мм} %ТекстДополнительный",
            МодульТелеграм.ВидыКлавиатуры.Подтверждение)
    ;

    пер Ответ = Запрос.Ответ
    //Ответ.Заголовки.Установить("Content-Type", "text/plain")
    Ответ.УстановитьТело(ОтветСтрокой)
;