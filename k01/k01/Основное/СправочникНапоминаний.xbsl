@ВПроекте
метод ОбработатьНапоминание(Поля: СтруктураПолученныхПолей): Строка
    знч Эл = СправочникНапоминаний.СоздатьНаОсновании(Поля)
    Эл.Записать()
    возврат Эл.Код
;


@ВПроекте
метод ПолучитьСписокНапоминаний(ИдЧата: Число): Массив<СтруктураПолученныхПолей>
    знч ТекДата = ДатаВремя.Сейчас(ЧасовойПояс.Текущий())

    исп Результат = Запрос{
        ВЫБРАТЬ ПЕРВЫЕ 10
            Код,
            ДатаНапоминания,
            СтрокаИсходная
        ИЗ
            СправочникНапоминаний
        ГДЕ
            ИдЧата == %ИдЧата
             И НЕ Выполнено
             И НЕ Отменено
             И ДатаНапоминания > %ТекДата
        УПОРЯДОЧИТЬ ПО
            ДатаНапоминания
    }.Выполнить()

    знч Данные =
        Результат.Преобразовать(Стр -> новый СтруктураПолученныхПолей(Код = Стр.Код, ДатаНапоминания = Стр
                    .ДатаНапоминания,
                СтрокаИсходная = Стр.СтрокаИсходная)).ВМассив()

    Результат.Закрыть()

    возврат Данные
;


@ВПроекте
метод НайтиНапоминание(ИдЧата: Число, ИдСообщения: Число): СправочникНапоминаний.Ссылка?
    исп Результат = Запрос{
        ВЫБРАТЬ
            Ссылка
        ИЗ
            СправочникНапоминаний
        ГДЕ
            ИдСообщения == %ИдСообщения
             И ИдЧата == %ИдЧата
    }.Выполнить()

    знч Ссылка = Результат.ЕдинственныйИлиУмолчание()?.Ссылка

    Результат.Закрыть()

    возврат Ссылка
;


@ВПроекте
метод ОтменитьНапоминание(Напоминание: СправочникНапоминаний.Ссылка?): Строка
    если Напоминание == Неопределено
        возврат ""
    ;

    знч Эл = Напоминание.ЗагрузитьОбъект()

    новый ExecutionEvent(Details = "${Эл.ИдЧата}, в ${Эл.ДатаРегистрации|дд.ММ.гггг ЧЧ:мм} вы отменили: %{Эл
            .СтрокаИсходная}").Записать()

    Эл.Отменено = Истина
    Эл.Записать(ПараметрыЗаписи = новый СправочникНапоминаний.ПараметрыЗаписи(ЗаписьИзСобытия = Ложь))

    возврат "Отменена ${Эл.ДатаНапоминания|дд.ММ.гггг ЧЧ:мм}: %{Эл.СтрокаИсходная}"
;


@ВПроекте
метод ВызватьНапоминание(КодНапоминания: Строка): Строка
    знч Эл = СправочникНапоминаний.НайтиПоКоду(КодНапоминания).ЗагрузитьОбъект()

    новый ExecutionEvent(Details = "${Эл.ИдЧата}, в ${Эл.ДатаРегистрации|дд.ММ.гггг ЧЧ:мм} вы написали: %{Эл
            .СтрокаИсходная}").Записать()

    знч ОтветСообщения = МодульТелеграм.ОтправитьТекстовоеСообщение(Эл.ИдЧата,
        "В ${Эл.ДатаРегистрации|дд.ММ.гггг ЧЧ:мм} вы написали: 
    %{Эл.СтрокаИсходная}", МодульТелеграм.ПолучитьКлавиатуруПродление())

    Эл.Выполнено = Истина
    Эл.Записать(ПараметрыЗаписи = новый СправочникНапоминаний.ПараметрыЗаписи(ЗаписьИзСобытия = Истина))

    возврат ОтветСообщения
;