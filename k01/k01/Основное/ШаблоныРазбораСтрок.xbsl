конст РАЗДЕЛИТЕЛЬ_ДЛЯ_РАЗБОРА_СТРОК = "#&#"


@ВПроекте
структура ШаблонРазбора
    обз пер РегВыражение: Строка
    обз пер Заменитель: Строка
    пер Комментарий: Строка
        
    @ВПроекте
    статический метод Из(РегВыражение:Строка): ШаблонРазбора
        возврат новый  ШаблонРазбора(РегВыражение,"")
    ;
    
    @ВПроекте
    статический метод Из(РегВыражение:Строка, Заменитель:Строка): ШаблонРазбора
        возврат новый  ШаблонРазбора(РегВыражение,Заменитель)
    ;
		
	метод Разобрать(Источник: Строка): Строка
        знч Результат = Источник.Заменить(новый Образец("%РегВыражение(?im)"), Заменитель) 
        возврат Результат
	;


    метод Разделить(Источник: Строка): РазделеннаяСтрока
        пер РазобрСтрока:Строка
        если Заменитель.Пусто() 
            РазобрСтрока = ШаблонРазбора.Из(РегВыражение,"$1%{РАЗДЕЛИТЕЛЬ_ДЛЯ_РАЗБОРА_СТРОК}$2").Разобрать(Источник) 
        иначе    
            РазобрСтрока = этот.Разобрать(Источник) 
        ;
        пер Рез = РазобрСтрока.Разделить(РАЗДЕЛИТЕЛЬ_ДЛЯ_РАЗБОРА_СТРОК)          
        если Рез.Размер() == 1
            возврат новый РазделеннаяСтрока(Источник,"") 
        иначе 
            возврат новый РазделеннаяСтрока(Рез[0],Рез[1])
        ;        
	;

	метод РазделитьУпрощенноНа2Группы(Источник: Строка): РазделеннаяСтрока
        
        знч ОбразецДляПоискаСочетаний = новый Образец("%РегВыражение(?im)")
        знч РезультатПоиска = ОбразецДляПоискаСочетаний.НайтиСовпадения(Источник)
        если РезультатПоиска.Пусто() 
            возврат новый РазделеннаяСтрока(Источник,"") 
        иначе 
            знч ПервыйРезультат = РезультатПоиска[0]
            возврат новый РазделеннаяСтрока(ПервыйРезультат.Группа(1),ПервыйРезультат.Группа(2))
        ;        
	;
;

@ВПроекте
структура РазделеннаяСтрока
    обз пер Начало: Строка
    обз пер Конец: Строка
;


@ВПроекте
метод РазобратьМассив(МассивШаблонов: Массив<ШаблонРазбора>, Источник: Строка): Строка
    для Элемент из МассивШаблонов    
        Источник = Элемент.Разобрать(Источник)    
    ;
    возврат Источник 
;        

@ВПроекте
метод РазделитьМассив(МассивШаблонов: Массив<ШаблонРазбора>, Источник: Строка): РазделеннаяСтрока
    пер Результат = новый РазделеннаяСтрока(Источник,"")
    для Элемент из МассивШаблонов    
        знч Разд:РазделеннаяСтрока = Элемент.Разделить(Источник)    
        если не Разд.Конец.Пусто() 
            Результат = Разд
            прервать
        ;    
    ;
    возврат Результат
;        


@ВПроекте
метод ПолучитьШаблоныВыделенияСтрокиСДатой():Массив<ШаблонРазбора>
    // считаем, что дата время указана в конце сообщения в форме !когда! в !какое время! или !через! !дней, часов или минут!

    пер МассивШаблонов: Массив<ШаблонРазбора> = новый Массив<ШаблонРазбора>()
    МассивШаблонов.Добавить(ШаблонРазбора.Из("(.*)( дата .*)"))
    МассивШаблонов.Добавить(ШаблонРазбора.Из("(.*?)( через .*)"))
    МассивШаблонов.Добавить(ШаблонРазбора.Из("(.*)((в|на) следующ.*)"))
    МассивШаблонов.Добавить(ШаблонРазбора.Из("(.*)((сегодня|завтра|послезавтра).*)"))
    возврат МассивШаблонов
;


@ВПроекте
метод ПолучитьШаблоныПредварительнойОбработки():Массив<ШаблонРазбора>
    пер МассивШаблонов: Массив<ШаблонРазбора> = новый Массив<ШаблонРазбора>()
    МассивШаблонов.Добавить(ШаблонРазбора.Из(" утром"," в 10:00"))
    МассивШаблонов.Добавить(ШаблонРазбора.Из(" днем"," в 13:00"))
    МассивШаблонов.Добавить(ШаблонРазбора.Из(" вечером"," в 22:00"))

    МассивШаблонов.Добавить(ШаблонРазбора.Из(" в час дня"," в 13:00"))
    МассивШаблонов.Добавить(ШаблонРазбора.Из(" в час ночи"," в 1:00"))

    //
    // МассивШаблонов.Добавить(ШаблонРазбора.Из(" в (\d?) час?"," в %1:00"))
    
    // без четверти, в четверть, в половине первого, полпервого, без пятнадцати, пятнадцать минут первого

	МассивШаблонов.Добавить(ШаблонРазбора.Из("(?:2|две)\\s+?тысячи\\s+?(?=два|три|чет)", " ")) // уберем тысячи из года (надо бы проверить, что это именно дата)
    
	МассивШаблонов.Добавить(ШаблонРазбора.Из("после завтра", "послезавтра")) // на случай ошибки
	
    МассивШаблонов.Добавить(ШаблонРазбора.Из("ближайший", "следующий")) // синонимы
    МассивШаблонов.Добавить(ШаблонРазбора.Из("ближайшую", "следующую")) // синонимы
    МассивШаблонов.Добавить(ШаблонРазбора.Из("ближайшее", "следующее")) // синонимы
    
	МассивШаблонов.Добавить(ШаблонРазбора.Из("в понедельник","в следующий понедельник"))
	МассивШаблонов.Добавить(ШаблонРазбора.Из("во вторник","в следующий вторник"))
	МассивШаблонов.Добавить(ШаблонРазбора.Из("в среду","в следующую среду"))
	МассивШаблонов.Добавить(ШаблонРазбора.Из("в четверг","в следующий четверг"))
	МассивШаблонов.Добавить(ШаблонРазбора.Из("в пятницу","в следующую пятницу"))
	МассивШаблонов.Добавить(ШаблонРазбора.Из("в субботу","в следующую субботу"))
	МассивШаблонов.Добавить(ШаблонРазбора.Из("в воскресенье","в следующее воскресенье"))
    
    возврат МассивШаблонов
;


@Локально
метод СтроковыеПредставленияЧисел(): Соответствие<Строка, Строка>
    знч СтроковыеПредставленияПолейТек = новый Соответствие<Строка, Строка>()
    СтроковыеПредставленияПолейТек.Вставить("ноль", "0")
    СтроковыеПредставленияПолейТек.Вставить("нуль", "0")
    СтроковыеПредставленияПолейТек.Вставить("один", "1")
    СтроковыеПредставленияПолейТек.Вставить("одна", "1")
    СтроковыеПредставленияПолейТек.Вставить("два", "2")
    СтроковыеПредставленияПолейТек.Вставить("две", "2")
    СтроковыеПредставленияПолейТек.Вставить("три", "3")
    СтроковыеПредставленияПолейТек.Вставить("четыре", "4")
    СтроковыеПредставленияПолейТек.Вставить("пять", "5")
    СтроковыеПредставленияПолейТек.Вставить("шесть", "6")
    СтроковыеПредставленияПолейТек.Вставить("семь", "7")
    СтроковыеПредставленияПолейТек.Вставить("восемь", "8")
    СтроковыеПредставленияПолейТек.Вставить("девять", "9")
    СтроковыеПредставленияПолейТек.Вставить("десять", "10")
    СтроковыеПредставленияПолейТек.Вставить("одиннадцат(?:ь|ое|ого)", "11")
    СтроковыеПредставленияПолейТек.Вставить("двенадцат(?:ь|ое|ого)", "12")
    СтроковыеПредставленияПолейТек.Вставить("тринадцат(?:ь|ое|ого)", "13")
    СтроковыеПредставленияПолейТек.Вставить("четырнадцат(?:ь|ое|ого)", "14")
    СтроковыеПредставленияПолейТек.Вставить("пятнадцат(?:ь|ое|ого)", "15")
    СтроковыеПредставленияПолейТек.Вставить("шестнадцат(?:ь|ое|ого)", "16")
    СтроковыеПредставленияПолейТек.Вставить("семнадцат(?:ь|ое|ого)", "17")
    СтроковыеПредставленияПолейТек.Вставить("восемнадцат(?:ь|ое|ого)", "18")
    СтроковыеПредставленияПолейТек.Вставить("девятнадцат(?:ь|ое|ого)", "19")
    СтроковыеПредставленияПолейТек.Вставить("двадцат(?:ь|ое|ого)", "20")
    СтроковыеПредставленияПолейТек.Вставить("тридцат(?:ь|ое|ого)", "30")
    СтроковыеПредставленияПолейТек.Вставить("соро(?:к|ковое|кового)", "40")
    СтроковыеПредставленияПолейТек.Вставить("пятьдеся(?:т|тое|того)", "50")
    СтроковыеПредставленияПолейТек.Вставить("шестьдеся(?:т|тое|того)", "60")
    СтроковыеПредставленияПолейТек.Вставить("перво(?:е|го)", "1")
    СтроковыеПредставленияПолейТек.Вставить("второ(?:е|го)", "2")
    СтроковыеПредставленияПолейТек.Вставить("треть(?:е|его)", "3")
    СтроковыеПредставленияПолейТек.Вставить("четверто(?:е|го)", "4")
    СтроковыеПредставленияПолейТек.Вставить("пято(?:е|го)", "5")
    СтроковыеПредставленияПолейТек.Вставить("шесто(?:е|го)", "6")
    СтроковыеПредставленияПолейТек.Вставить("седьмо(?:е|го)", "7")
    СтроковыеПредставленияПолейТек.Вставить("восьмо(?:е|го)", "8")
    СтроковыеПредставленияПолейТек.Вставить("девято(?:е|го)", "9")
    СтроковыеПредставленияПолейТек.Вставить("десято(?:е|го)", "10")
    СтроковыеПредставленияПолейТек.Вставить("янв.*?", "1")
    СтроковыеПредставленияПолейТек.Вставить("фев.*?", "2")
    СтроковыеПредставленияПолейТек.Вставить("март.*?", "3")
    СтроковыеПредставленияПолейТек.Вставить("апр.*?", "4")
    СтроковыеПредставленияПолейТек.Вставить("мая", "5")
    СтроковыеПредставленияПолейТек.Вставить("июн.*?", "6")
    СтроковыеПредставленияПолейТек.Вставить("июл.*?", "7")
    СтроковыеПредставленияПолейТек.Вставить("авг.*?", "8")
    СтроковыеПредставленияПолейТек.Вставить("сент.*?", "9")
    СтроковыеПредставленияПолейТек.Вставить("окт.*?", "10")
    СтроковыеПредставленияПолейТек.Вставить("ноя.*?", "11")
    СтроковыеПредставленияПолейТек.Вставить("дек.*?", "12")
    
    возврат СтроковыеПредставленияПолейТек
;


@ВПроекте
метод ПолучитьШаблоныСтроковыхПредставленийЧисел(): Массив<ШаблонРазбора>
    пер МассивШаблонов: Массив<ШаблонРазбора> = новый Массив<ШаблонРазбора>()
    знч СтроковыеПредставленияЧисел = СтроковыеПредставленияЧисел()
    для Эл из СтроковыеПредставленияЧисел
        МассивШаблонов.Добавить(ШаблонРазбора.Из("(?:\\s|^)(%{Эл.Ключ})(?:-|:|;|,|\\.|\\s|$)", " ${Эл.Значение} "))
    ;

    возврат МассивШаблонов
;